//@version=2
study(title="ScannerData", shorttitle="_SD_")


// study(title="Average True Range", shorttitle="ATR", overlay=false)
plot(rma(tr(true), 14), title="ATR")


// study("Average Directional Index", shorttitle="ADX")
dirmov(len) =>
	up = change(high)
	down = -change(low)
	truerange = rma(tr, len)
	plus = fixnan(100 * rma(up > down and up > 0 ? up : 0, len) / truerange)
	minus = fixnan(100 * rma(down > up and down > 0 ? down : 0, len) / truerange)
	[plus, minus]

adx(dilen, adxlen) => 
	[plus, minus] = dirmov(dilen)
	sum = plus + minus
	adx = 100 * rma(abs(plus - minus) / (sum == 0 ? 1 : sum), adxlen)

plot(adx(14, 14), title="ADX")


// study(shorttitle="BB", title="Bollinger Bands", overlay=true)
lengthBB = 20
srcBB = close
multBB = 2
basisBB = sma(srcBB, lengthBB)
devBB = multBB * stdev(srcBB, lengthBB)
upperBB = basisBB + devBB
lowerBB = basisBB - devBB
plot(basisBB, title="BB.basis")
plot(upperBB, title="BB.upper")
plot(lowerBB, title="BB.lower")


// study(title="Moving Average Convergence/Divergence", shorttitle="MACD")
sourceMACD = close
fastLengthMACD = 12
slowLengthMACD = 26
signalLengthMACD = 9
fastMAMACD = ema(sourceMACD, fastLengthMACD)
slowMAMACD = ema(sourceMACD, slowLengthMACD)
macdMACD = fastMAMACD - slowMAMACD
signalMACD = sma(macdMACD, signalLengthMACD)
histMACD = macdMACD - signalMACD
plot(histMACD, title="MACD.hist")
plot(macdMACD, title="MACD.macd")
plot(signalMACD, title="MACD.signal")


//study(title="Momentum", shorttitle="Mom")
plot(close - close[10], title="Mom")


//study(title="Relative Strength Index", shorttitle="RSI")
plot(rsi(close,14), title="RSI")
plot(rsi(close,7), title="RSI7")


// study(title="Moving Average", shorttitle="MA", overlay=true)
plot(sma(close, 10), title="SMA10")
plot(sma(close, 20), title="SMA20")
plot(sma(close, 30), title="SMA30")
plot(sma(close, 50), title="SMA50")
plot(sma(close, 100), title="SMA100")
plot(sma(close, 200), title="SMA200")

// study(title="Exponential Moving Average", shorttitle="EMA", overlay=true)
plot(ema(close, 10), title="EMA10")
plot(ema(close, 20), title="EMA20")
plot(ema(close, 30), title="EMA30")
plot(ema(close, 50), title="EMA50")
plot(ema(close, 100), title="EMA100")
plot(ema(close, 200), title="EMA200")

// study(title="Stochastic", shorttitle="Stoch")
lengthStoch = 14
smoothKStoch = 1
smoothDStoch = 3
kStoch = sma(stoch(close, high, low, lengthStoch), smoothKStoch)
dStoch = sma(kStoch, smoothDStoch)
plot(kStoch, title="Stoch.K")
plot(dStoch, title="Stoch.D")

// performance
rateOfreturn(v1, v2) => (v1 - v2) * 100 / v2
rr(bb) =>
    rof = rateOfreturn(close, close[bb])
    rof[1]
plot(rr(5), title="Perf.W")
plot(rr(21), title="Perf.1M")
plot(rr(63), title="Perf.3M")
plot(rr(126), title="Perf.6M")
plot(rr(252), title="Perf.Y")
lastYearClose = lastYearClose[1]
if year > year[1]
    lastYearClose := close[1]
plot(rateOfreturn(close, lastYearClose)[1], title="Perf.YTD")

// volatility
volatility(bb) => sma((high-low)/low * 100, bb)
plot(volatility(5), title="Volatility.W")
plot(volatility(21), title="Volatility.M")
plot(tr(true)*100/low, title="Volatility.D")

// average volume
plot(sma(volume,90*5/7)[1], title="average_volume")

// high & low
allTimeHigh = max(nz(allTimeHigh[1], high), high)
allTimeLow = min(nz(allTimeLow[1], low), low)
plot(allTimeHigh, title="High.All")
plot(allTimeLow, title="Low.All")
plot(nz(highest(high, 52*5), allTimeHigh), title="price_52_week_high")
plot(nz(lowest(low, 52*5), allTimeLow), title="price_52_week_low")
plot(nz(highest(high, 126), allTimeHigh), title="High.6M")
plot(nz(lowest(low, 126), allTimeLow), title="Low.6M")
plot(nz(highest(high, 63), allTimeHigh), title="High.3M")
plot(nz(lowest(low, 63), allTimeLow), title="Low.3M")
plot(nz(highest(high, 21), allTimeHigh), title="High.1M")
plot(nz(lowest(low, 21), allTimeLow), title="Low.1M")

// Commodity Channel Index
plot(cci(close, 20), title = "CCI20")

// Donchian Channels
donchLen = 20
donchUpper = highest(donchLen)
donchLower = lowest(donchLen)
plot(donchLower, title = "DonchCh20.Lower")
plot(donchUpper, title = "DonchCh20.Upper")
plot(avg(donchUpper, donchLower), title="DonchCh20.Middle")

// Hull Moving Average
hullMALength = 9
hullMA_n2ma=2*wma(close,round(hullMALength/2))
hullMA_nma=wma(close,hullMALength)
plot(wma(hullMA_n2ma - hullMA_nma, round(sqrt(hullMALength))), title = "HullMA9")

// Awesome Oscillator
plot(sma(hl2, 5) - sma(hl2, 34), title = "AO")

// Pivot Points
pivotM_open = nz(pivotM_open[1],open)
pivotM_high = nz(pivotM_high[1],high)
pivotM_low = nz(pivotM_low[1],low)

pivotM_prev_open = nz(pivotM_prev_open[1])
pivotM_prev_high = nz(pivotM_prev_high[1])
pivotM_prev_low = nz(pivotM_prev_low[1])
pivotM_prev_close = nz(pivotM_prev_close[1])

pivotM_Month = month(time)
if pivotM_Month != pivotM_Month[1]
    pivotM_prev_open := pivotM_open
    pivotM_prev_close := close[1]
    pivotM_prev_high := pivotM_high
    pivotM_prev_low := pivotM_low

    pivotM_open := open
    pivotM_high := high
    pivotM_low := low
else
    pivotM_high := max(pivotM_high, high)
    pivotM_low := min(pivotM_low, low)

// Classic
pivotM_Median = (pivotM_prev_high + pivotM_prev_low + pivotM_prev_close)/3
plot(pivotM_Median, title = 'Pivot.M.Classic.Middle')
plot(pivotM_Median * 2 - pivotM_prev_low, title = 'Pivot.M.Classic.R1')
plot(pivotM_Median * 2 - pivotM_prev_high, title = 'Pivot.M.Classic.S1')
plot(pivotM_Median + 1*(pivotM_prev_high - pivotM_prev_low), title = 'Pivot.M.Classic.R2')
plot(pivotM_Median - 1*(pivotM_prev_high - pivotM_prev_low), title = 'Pivot.M.Classic.S2')
plot(pivotM_Median + 2*(pivotM_prev_high - pivotM_prev_low), title = 'Pivot.M.Classic.R3')
plot(pivotM_Median - 2*(pivotM_prev_high - pivotM_prev_low), title = 'Pivot.M.Classic.S3')

// Fibonacci
plot(pivotM_Median, title = 'Pivot.M.Fibonacci.Middle')
plot(pivotM_Median + 0.382 * (pivotM_prev_high - pivotM_prev_low), title = 'Pivot.M.Fibonacci.R1')
plot(pivotM_Median - 0.382 * (pivotM_prev_high - pivotM_prev_low), title = 'Pivot.M.Fibonacci.S1')
plot(pivotM_Median + 0.618 * (pivotM_prev_high - pivotM_prev_low), title = 'Pivot.M.Fibonacci.R2')
plot(pivotM_Median - 0.618 * (pivotM_prev_high - pivotM_prev_low), title = 'Pivot.M.Fibonacci.S2')
plot(pivotM_Median + 1 * (pivotM_prev_high - pivotM_prev_low), title = 'Pivot.M.Fibonacci.R3')
plot(pivotM_Median - 1 * (pivotM_prev_high - pivotM_prev_low), title = 'Pivot.M.Fibonacci.S3')

// Woodie
pivotM_Woodie_Median = (pivotM_prev_high + pivotM_prev_low + pivotM_prev_open * 2)/4
plot(pivotM_Woodie_Median, title = 'Pivot.M.Woodie.Middle')
plot(pivotM_Woodie_Median * 2 - pivotM_prev_low, title = 'Pivot.M.Woodie.R1')
plot(pivotM_Woodie_Median * 2 - pivotM_prev_high, title = 'Pivot.M.Woodie.S1')
plot(pivotM_Woodie_Median + 1*(pivotM_prev_high - pivotM_prev_low), title = 'Pivot.M.Woodie.R2')
plot(pivotM_Woodie_Median - 1*(pivotM_prev_high - pivotM_prev_low), title = 'Pivot.M.Woodie.S2')
plot(pivotM_prev_high + 2*(pivotM_Woodie_Median - pivotM_prev_low), title = 'Pivot.M.Woodie.R3')
plot(pivotM_prev_low - 2*(pivotM_prev_high - pivotM_Woodie_Median), title = 'Pivot.M.Woodie.S3')

// Demark
pivotM_Demark_X = pivotM_prev_high + pivotM_prev_low*2 + pivotM_prev_close
if pivotM_prev_close == pivotM_prev_open
    pivotM_Demark_X := pivotM_prev_high + pivotM_prev_low + pivotM_prev_close*2
if pivotM_prev_close > pivotM_prev_open
    pivotM_Demark_X := pivotM_prev_high*2 + pivotM_prev_low + pivotM_prev_close
plot(pivotM_Demark_X/4, title = 'Pivot.M.Demark.Middle')
plot(pivotM_Demark_X/2 - pivotM_prev_low, title = 'Pivot.M.Demark.R1')
plot(pivotM_Demark_X/2 - pivotM_prev_high, title = 'Pivot.M.Demark.S1')

// Camarilla
plot(pivotM_Median, title = 'Pivot.M.Camarilla.Middle')
plot(pivotM_prev_close + 1.1/12.0*(pivotM_prev_high - pivotM_prev_low), title = 'Pivot.M.Camarilla.R1')
plot(pivotM_prev_close - 1.1/12.0*(pivotM_prev_high - pivotM_prev_low), title = 'Pivot.M.Camarilla.S1')
plot(pivotM_prev_close + 1.1/6.0*(pivotM_prev_high - pivotM_prev_low), title = 'Pivot.M.Camarilla.R2')
plot(pivotM_prev_close - 1.1/6.0*(pivotM_prev_high - pivotM_prev_low), title = 'Pivot.M.Camarilla.S2')
plot(pivotM_prev_close + 1.1/4.0*(pivotM_prev_high - pivotM_prev_low), title = 'Pivot.M.Camarilla.R3')
plot(pivotM_prev_close - 1.1/4.0*(pivotM_prev_high - pivotM_prev_low), title = 'Pivot.M.Camarilla.S3')


//////////////////////////////////// Candle Patterns ////////////////////////////////////

C_Len = (14)
C_EmaLen = (50)
C_Factor = (3.0)
C_ShadowPercent = (5)

C_BodyHi = max( close, open )
C_BodyLo = min( close, open )
C_Body = C_BodyHi - C_BodyLo
C_PriceAvg = ema( close, C_EmaLen )
C_BodyAvg = ema( C_Body, C_Len )
C_SmallBody = C_Body < C_BodyAvg
C_LongBody = C_Body > C_BodyAvg
C_UpShadow = high - C_BodyHi
C_DnShadow = C_BodyLo - low
C_DownTrend = close < C_PriceAvg
C_UpTrend = close > C_PriceAvg
C_HasUpShadow = C_UpShadow > C_ShadowPercent / 100 * C_Body
C_HasDnShadow = C_DnShadow > C_ShadowPercent / 100 * C_Body
C_WhiteBody = open < close
C_BlackBody = open > close
C_Range = high-low
C_IsInsideBar = high[1] > high and low[1] < low

// study("Hammer, Inverted Hammer, Hanging Man, Shooting Star")
C_oHammer = false
C_oHangingMan = false
if C_SmallBody and C_Body > 0 and C_BodyLo > hl2 and C_DnShadow >= C_Factor * C_Body and not C_HasUpShadow
    if C_DownTrend
        C_oHammer := true
	else
	    if C_UpTrend
	        C_oHangingMan := true

plot(C_oHammer, title = "Candle.Hammer")
plot(C_oHangingMan, title = "Candle.HangingMan")

C_oInvertedHammer = false
C_oShootingStar = false
if C_SmallBody and C_Body > 0 and C_BodyHi < hl2 and C_UpShadow >= C_Factor * C_Body and not C_HasDnShadow
    if C_DownTrend
        C_oInvertedHammer := true
	else
	    if C_UpTrend
	        C_oShootingStar := true

plot(C_oInvertedHammer, title = "Candle.InvertedHammer")
plot(C_oShootingStar, title = "Candle.ShootingStar")


// study("Evening & Morning Stars")
C_oMorningStar = false
C_oEveningStar = false
if close[2] < open[2] and C_Body[2] > C_BodyAvg[2] and C_BodyHi[1] < C_BodyLo[2] and C_Body[1] < C_BodyAvg[1] and close > open and C_BodyHi > C_BodyLo[2]
    C_oMorningStar := true
else
    if close[2] > open[2] and C_Body[2] > C_BodyAvg[2] and C_BodyLo[1] > C_BodyHi[2] and C_Body[1] < C_BodyAvg[1]	and close < open and C_BodyLo < C_BodyHi[2]
        C_oEveningStar := true
plot(C_oMorningStar, title = "Candle.MorningStar")
plot(C_oEveningStar, title = "Candle.EveningStar")


// study("Marubozu")
C_MarubozuShadowPercent = (5)
C_Marubozu = C_LongBody and C_UpShadow <= C_MarubozuShadowPercent/100*C_Body and C_DnShadow <= C_MarubozuShadowPercent/100*C_Body
C_WhiteMarubozu = C_Marubozu and C_WhiteBody
C_BlackMarubozu = C_Marubozu and C_BlackBody
plot(C_BlackMarubozu, title = "Candle.Marubozu.Black")
plot(C_WhiteMarubozu, title = "Candle.Marubozu.White")


//study("Doji")
C_DojiBodyPercent = (5)
C_Doji = C_Range > 0 and C_Body <= C_Range * C_DojiBodyPercent / 100
C_DragonflyDoji = C_Doji and C_DownTrend and C_UpShadow <= C_Body
C_GravestoneDoji = C_Doji and C_UpTrend and C_DnShadow <= C_Body
plot(C_Doji and not C_DragonflyDoji and not C_GravestoneDoji, title = "Candle.Doji")
plot(C_DragonflyDoji, title = "Candle.Doji.Dragonfly")
plot(C_GravestoneDoji, title = "Candle.Doji.Gravestone")


// study("Harami")
plot(C_LongBody[1] and C_BlackBody[1] and C_DownTrend[1] and C_Doji and C_IsInsideBar, title = "Candle.Harami.Bullish")
plot(C_LongBody[1] and C_WhiteBody[1] and C_UpTrend[1] and C_Doji and C_IsInsideBar, title = "Candle.Harami.Bearish")


//study("Long Shadows")
C_LongShadowPercent = (75)
plot(C_DnShadow >= C_Range/100*C_LongShadowPercent, title = "Candle.LongShadow.Lower")
plot(C_UpShadow >= C_Range/100*C_LongShadowPercent, title = "Candle.LongShadow.Upper")


//study("Spinning Top")
C_SpinningTopPercent = (25)
C_IsSpinningTop = C_DnShadow >= C_Range/100*C_SpinningTopPercent and C_UpShadow >= C_Range/100*C_SpinningTopPercent
plot(C_IsSpinningTop and C_WhiteBody, title = "Candle.SpinningTop.White")
plot(C_IsSpinningTop and C_BlackBody, title = "Candle.SpinningTop.Black")


// study("3 White Soldiers & 3 Black Crows")
C_3WSld3WCrwShadowPercent = (5)
C_3WSld3WCrw_HaveNotUpShadow = C_Range * C_3WSld3WCrwShadowPercent / 100 > C_UpShadow
C_3WSld3WCrw_HaveNotDnShadow = C_Range * C_3WSld3WCrwShadowPercent / 100 > C_DnShadow
C_o3WhiteSoldiers = false
C_o3BlackCrows = false
if C_LongBody and C_LongBody[1] and C_LongBody[2]
    if C_WhiteBody and C_WhiteBody[1] and C_WhiteBody[2]
        C_o3WhiteSoldiers := close > close[1] and close[1] > close[2] and open < close[1] and open > open[1] and open[1] < close[2] and open[1] > open[2] and C_3WSld3WCrw_HaveNotUpShadow and C_3WSld3WCrw_HaveNotUpShadow[1] and C_3WSld3WCrw_HaveNotUpShadow[2]
    if C_BlackBody and C_BlackBody[1] and C_BlackBody[2]
        C_o3BlackCrows := close < close[1] and close[1] < close[2] and open > close[1] and open < open[1] and open[1] > close[2] and open[1] < open[2] and C_3WSld3WCrw_HaveNotDnShadow and C_3WSld3WCrw_HaveNotDnShadow[1] and C_3WSld3WCrw_HaveNotDnShadow[2]
plot(C_o3WhiteSoldiers, title = "Candle.3WhiteSoldiers")
plot(C_o3BlackCrows, title = "Candle.3BlackCrows")


// study("Engulfing")
plot(C_DownTrend and C_WhiteBody and C_LongBody and C_BlackBody[1] and C_SmallBody[1] and close >= open[1] and open <= close[1] and ( close > open[1] or open < close[1] ), title = "Candle.Engulfing.Bullish")
plot(C_UpTrend and C_BlackBody and C_LongBody and C_WhiteBody[1] and C_SmallBody[1] and close <= open[1] and open >= close[1] and ( close < open[1] or open > close[1] ), title = "Candle.Engulfing.Bearish")


// study("Abandoned Baby")
plot(C_DownTrend[2] and C_BlackBody[2] and C_Doji[1] and low[2] > high[1] and C_WhiteBody and high[1] < low, title = "Candle.AbandonedBaby.Bullish")
plot(C_UpTrend[2] and C_WhiteBody[2] and C_Doji[1] and high[2] < low[1] and C_BlackBody and low[1] > high, title = "Candle.AbandonedBaby.Bearish")


// study("Tri-Star")
C_3Dojis = C_Doji[2] and C_Doji[1] and C_Doji
C_BodyGapUp = C_BodyHi[1] < C_BodyLo
C_BodyGapDn = C_BodyLo[1] > C_BodyHi
plot(C_3Dojis and C_DownTrend[2] and C_BodyGapDn[1] and C_BodyGapUp, title = "Candle.TriStar.Bullish")
plot(C_3Dojis and C_UpTrend[2] and C_BodyGapUp[1] and C_BodyGapDn, title = "Candle.TriStar.Bearish")


// study("Kicking")
plot(C_BlackMarubozu[1] and C_WhiteMarubozu and high[1] < low, title = "Candle.Kicking.Bullish")
plot(C_WhiteMarubozu[1] and C_BlackMarubozu and low[1] > high, title = "Candle.Kicking.Bearish")

// study("Aroon")
Aroon_length = (14)
plot(100 * (highestbars(high, Aroon_length+1) + Aroon_length)/Aroon_length, title="Aroon.Up")
plot(100 * (lowestbars(low, Aroon_length+1) + Aroon_length)/Aroon_length, title="Aroon.Down")